import multiversion.BasePlatformConfig
import multiversion.Dependency
import multiversion.VersionConfig
import org.gradle.api.tasks.SourceSetContainer
import org.gradle.api.tasks.compile.JavaCompile
import org.gradle.jvm.toolchain.JavaLanguageVersion
import org.gradle.jvm.toolchain.JavaToolchainService
import org.gradle.jvm.tasks.Jar
// Imported because in a Kotlin build script `java` resolves to the Gradle extension,
// which shadows the java.* package.
import java.util.zip.ZipFile

plugins {
    `java-library`
}

@Suppress("UNCHECKED_CAST")
val versionConfig = rootProject.extra["versionConfig"] as VersionConfig
@Suppress("UNCHECKED_CAST")
val getCurrentNeoFormVersion = rootProject.extra["getCurrentNeoFormVersion"] as () -> String
val imguiVersion = if (versionConfig.common.minecraftVersion >= "26.1") "1.92.0" else "1.90.0"

val isNewVersion = versionConfig.common.minecraftVersion > "1.20.1"
if (isNewVersion) {
    apply(plugin = "net.neoforged.moddev")
} else {
    apply(plugin = "net.neoforged.moddev.legacyforge")
}


if (isNewVersion) {
    extensions.configure<Any>("neoForge") {
        withGroovyBuilder {
            setProperty("neoFormVersion", versionConfig.common.neoFormVersion ?: getCurrentNeoFormVersion())
            val parchmentVersion = versionConfig.common.parchmentVersion
            if (parchmentVersion != null) {
                "parchment" {
                    val parts = parchmentVersion.split(":")
                    setProperty("minecraftVersion", parts[0])
                    setProperty("mappingsVersion", parts[1])
                }
            }
        }
    }
} else {
    extensions.configure<Any>("legacyForge") {
        withGroovyBuilder {
            setProperty("mcpVersion", versionConfig.common.minecraftVersion)
            val parchmentVersion = versionConfig.common.parchmentVersion
            if (parchmentVersion != null) {
                "parchment" {
                    val parts = parchmentVersion.split(":")
                    setProperty("minecraftVersion", parts[0])
                    setProperty("mappingsVersion", parts[1])
                }
            }
        }
    }
}

repositories {
    maven {
        name = "Modrinth"
        url = uri("https://api.modrinth.com/maven")
        content {
            includeGroup("maven.modrinth")
        }
    }
    maven {
        name = "yoga"
        url = uri("https://repo1.maven.org/maven2")
        content {
            includeGroup("org.appliedenergistics.yoga")
        }
    }
    flatDir {
        dirs("../libs")
    }
}

fun findIris(config: BasePlatformConfig?): Pair<Dependency, Boolean>? {
    if (config == null) return null

    config.dependencies?.modrinth?.forEach { dep ->
        if (dep.name.trim() == "iris" || dep.name.trim() == "oculus") {
            return dep to false
        }
    }

    config.dependencies?.local?.forEach { dep ->
        if (dep.name.contains("iris") || dep.name.contains("oculus")) {
            return dep to true
        }
    }

    return null
}
fun findFirstConfiguration(vararg names: String): String {
    return names.firstOrNull { name -> configurations.findByName(name) != null } ?: names.last()
}
fun DependencyHandler.modCompileOnlyCompat(notation: Any) =
    add(findFirstConfiguration("modCompileOnly", "compileOnly"), notation)


dependencies {
    compileOnly("org.anarres:jcpp:1.4.14")
    compileOnly("org.spongepowered:mixin:0.8.7")
    compileOnly("io.github.spair:imgui-java-app:$imguiVersion")
    compileOnly("io.github.spair:imgui-java-binding:$imguiVersion")
    compileOnly("io.github.spair:imgui-java-lwjgl3:$imguiVersion")
    compileOnly("org.lwjgl:lwjgl-vulkan:${versionConfig.common.lwjglVersion}")
    compileOnly("org.lwjgl:lwjgl-vma:${versionConfig.common.lwjglVersion}")

    compileOnly("com.electronwill.night-config:toml:3.8.3")
    compileOnly("com.electronwill.night-config:core:3.8.3")
    compileOnly("net.neoforged:bus:8.0.5")

    testImplementation("org.junit.jupiter:junit-jupiter:5.10.2")
    testImplementation("org.lwjgl:lwjgl:${versionConfig.common.lwjglVersion}")
    testImplementation("org.lwjgl:lwjgl-opengl:${versionConfig.common.lwjglVersion}")
    testImplementation("net.java.dev.jna:jna:5.14.0")
    testImplementation("it.unimi.dsi:fastutil:8.5.13")
    testImplementation("org.joml:joml:1.10.8")
    testImplementation("org.slf4j:slf4j-api:2.0.12")
    testImplementation("org.apache.logging.log4j:log4j-api:2.22.1")
    testImplementation("net.neoforged:bus:8.0.5")
    testRuntimeOnly("org.apache.logging.log4j:log4j-core:2.22.1")
    testRuntimeOnly("org.apache.logging.log4j:log4j-slf4j2-impl:2.22.1")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    if (versionConfig.common.minecraftVersion <= "1.21.1") {
        compileOnly("org.ow2.asm:asm:9.7.1")
        compileOnly("org.ow2.asm:asm-tree:9.7.1")
    } else {
        compileOnly("org.ow2.asm:asm:9.6")
        compileOnly("org.ow2.asm:asm-tree:9.6")
    }

    //Finding Iris dependency
    var irisDependency: Pair<Dependency, Boolean>? = null
    var irisPlatform: String? = null
    if (versionConfig.common.enableNeoForge && irisDependency == null) {
        irisDependency = findIris(versionConfig.neoforge)
        irisPlatform = "neoforge"
    }

    if (versionConfig.common.enableForge && irisDependency == null) {
        irisDependency = findIris(versionConfig.forge)
        irisPlatform = "forge"
    }

    if (versionConfig.common.enableFabric && irisDependency == null) {
        irisDependency = findIris(versionConfig.fabric)
        irisPlatform = "fabric"
    }

    if (irisDependency != null) {
        val dep = irisDependency.first
        if (irisDependency.second) {
            compileOnly(mapOf("name" to dep.name, "ext" to "jar"))
        } else {
            if (irisPlatform == "neoforge") {
                compileOnly(
                    "maven.modrinth:${dep.name}:${dep.version}-neo,${dep.minecraftVersion ?: versionConfig.common.minecraftVersion}"
                )
            } else {
                modCompileOnlyCompat(
                    "maven.modrinth:${dep.name}:${dep.version}-${irisPlatform},${dep.minecraftVersion ?: versionConfig.common.minecraftVersion}"
                )
            }
        }
    }
}

configurations {
    create("commonJava") {
        isCanBeResolved = false
        isCanBeConsumed = true
    }
    create("commonResources") {
        isCanBeResolved = false
        isCanBeConsumed = true
    }
}

val sourceSets = extensions.getByType(SourceSetContainer::class.java)
val javaToolchains = extensions.getByType(JavaToolchainService::class.java)
val mainSourceSet = sourceSets.getByName("main")
val irisapiSourceSet = sourceSets.maybeCreate("irisapi")
val sharedSourceSet = sourceSets.maybeCreate("shared")
val hackSourceSet = sourceSets.maybeCreate("hack")
val shaderCompatSourceSet = sourceSets.maybeCreate("shadercompat")

tasks.register<JavaCompile>("genJNIHeader") {
    description = "Generate JNI Header"
    val outputDir = file("../native/cpp/SRNativeMain/include")

    source = fileTree("../common/src/main/java") {
        include(
            "**/core/SuperResolutionNative.java",
            "**/core/streamline/StreamlineNative.java",
            "**/core/ngx/NgxNative.java",
            "**/thirdparty/nanovg/*.java"
        )
    }

    classpath = mainSourceSet.compileClasspath + mainSourceSet.output
    destinationDirectory.set(file("$buildDir/jni-temp"))
    javaCompiler.set(javaToolchains.compilerFor {
        languageVersion.set(JavaLanguageVersion.of(versionConfig.common.javaVersion))
    })
    options.headerOutputDirectory.set(outputDir)
    options.annotationProcessorPath = configurations.getByName("annotationProcessor")
    options.compilerArgs.addAll(listOf("-encoding", "UTF-8", "-proc:full"))

    doFirst {
        outputDir.mkdirs()
    }

    doLast {
        println("JNI headers generated at: ${outputDir.absolutePath}")
        delete("$buildDir/jni-temp")
    }
}

sharedSourceSet.annotationProcessorPath += mainSourceSet.annotationProcessorPath
sharedSourceSet.compileClasspath += mainSourceSet.compileClasspath
sharedSourceSet.runtimeClasspath += mainSourceSet.runtimeClasspath

irisapiSourceSet.annotationProcessorPath += mainSourceSet.annotationProcessorPath
irisapiSourceSet.compileClasspath += mainSourceSet.compileClasspath
irisapiSourceSet.runtimeClasspath += mainSourceSet.runtimeClasspath
irisapiSourceSet.compileClasspath += sharedSourceSet.output
irisapiSourceSet.runtimeClasspath += sharedSourceSet.output

mainSourceSet.compileClasspath += irisapiSourceSet.output
mainSourceSet.runtimeClasspath += irisapiSourceSet.output
mainSourceSet.compileClasspath += sharedSourceSet.output
mainSourceSet.runtimeClasspath += sharedSourceSet.output

hackSourceSet.annotationProcessorPath += mainSourceSet.annotationProcessorPath
hackSourceSet.compileClasspath += mainSourceSet.compileClasspath
hackSourceSet.compileClasspath += mainSourceSet.output
hackSourceSet.compileClasspath += sharedSourceSet.output
hackSourceSet.runtimeClasspath += mainSourceSet.runtimeClasspath
hackSourceSet.runtimeClasspath += mainSourceSet.output
hackSourceSet.runtimeClasspath += sharedSourceSet.output

shaderCompatSourceSet.annotationProcessorPath += mainSourceSet.annotationProcessorPath
shaderCompatSourceSet.compileClasspath += mainSourceSet.compileClasspath
shaderCompatSourceSet.compileClasspath += mainSourceSet.output
shaderCompatSourceSet.compileClasspath += sharedSourceSet.output
shaderCompatSourceSet.compileClasspath += irisapiSourceSet.output
shaderCompatSourceSet.runtimeClasspath += mainSourceSet.runtimeClasspath
shaderCompatSourceSet.runtimeClasspath += mainSourceSet.output
shaderCompatSourceSet.runtimeClasspath += sharedSourceSet.output
shaderCompatSourceSet.runtimeClasspath += irisapiSourceSet.output

val testSourceSet = sourceSets["test"]
testSourceSet.compileClasspath += mainSourceSet.compileClasspath
testSourceSet.runtimeClasspath += mainSourceSet.runtimeClasspath
testSourceSet.compileClasspath += mainSourceSet.output
testSourceSet.runtimeClasspath += mainSourceSet.output
testSourceSet.compileClasspath += sharedSourceSet.output
testSourceSet.runtimeClasspath += sharedSourceSet.output
testSourceSet.compileClasspath += irisapiSourceSet.output
testSourceSet.runtimeClasspath += irisapiSourceSet.output
testSourceSet.compileClasspath += hackSourceSet.output
testSourceSet.runtimeClasspath += hackSourceSet.output
testSourceSet.compileClasspath += shaderCompatSourceSet.output
testSourceSet.runtimeClasspath += shaderCompatSourceSet.output

tasks.named<Jar>("jar") {
    from(irisapiSourceSet.output)
    from(sharedSourceSet.output)
    from(hackSourceSet.output)
    from(shaderCompatSourceSet.output)
}

artifacts {
    listOf(mainSourceSet, irisapiSourceSet, sharedSourceSet, hackSourceSet, shaderCompatSourceSet).forEach { sourceSet ->
        sourceSet.java.sourceDirectories.files.forEach { dir ->
            add("commonJava", dir)
        }
        sourceSet.resources.sourceDirectories.files.forEach { dir ->
            add("commonResources", dir)
        }
    }
}

val useDebugLib = gradle.extensions.extraProperties.properties["isUseDebugLib"] as? Boolean == true

tasks.named<ProcessResources>("processResources") {
    if (useDebugLib) {
        exclude("**/libSuperResolution*+*+release.*")
    } else {
        exclude("**/libSuperResolution*+*+debug.*")
    }
}

tasks.withType<Test> {
    useJUnitPlatform()
    classpath = testSourceSet.runtimeClasspath
}

/*
 * API publishing.
 *
 * Only classes are published, without the bundled natives: the mod jar is ~44MB, of which
 * ~42MB is native libraries that a dependent mod never needs to compile against. The API
 * jar is a couple of MB, which matters because the publish matrix is one artifact per
 * Minecraft version per loader.
 *
 * This module is never remapped, so publishing from here also side-steps having to choose
 * between `jar` and Loom's `remapJar` depending on whether the target Minecraft version is
 * obfuscated.
 *
 * Development builds publish as -SNAPSHOT so they stay redeployable; the mod's own version
 * (which carries +dev.<commit> and the graphics backend, and is what the Modrinth and
 * CurseForge tasks consume) is deliberately left alone.
 */
apply(plugin = "maven-publish")

val srIsDevBuild = (gradle.extensions.extraProperties.properties["isDev"] as? Boolean) == true
val minecraftVersionConfig = providers.gradleProperty("minecraft_version_config").orNull
    ?: throw GradleException("Missing minecraft_version_config property")
val publishingApiToShnexus = gradle.startParameter.taskNames.any { taskName ->
    taskName.substringAfterLast(':') == "publishApiPublicationToShnexusRepository"
}

if (publishingApiToShnexus && minecraftVersionConfig != "1.20.1") {
    throw GradleException(
        "Remote Super Resolution API publishing must use minecraft_version_config=1.20.1; "
            + "run :publishApiToShnexus."
    )
}

// Resolved out here on purpose: inside the task configuration block `extensions` would
// resolve to the task's own container, since Task is ExtensionAware too.
val apiMainOutput = extensions.getByType<SourceSetContainer>().named("main").get().output

// Java 17, i.e. the 1.20.1 configuration. The published API is a single artifact shared
// by every Minecraft version that consumes it, so it has to be readable by the oldest
// toolchain among them.
val apiMaxClassFileMajor = 61
val apiSourceVersionConfig = "1.20.1"

val apiJar = tasks.register<Jar>("apiJar") {
    group = "publishing"
    description = "Classes-only jar for mods compiling against the Super Resolution API"
    archiveClassifier.set("api")
    from(apiMainOutput)
    exclude("lib/**")

    // The API is version-independent - its public signatures are identical across every
    // supported Minecraft version - but its class files are not: each version compiles at
    // its own java_version (17, 21, 25). A newer class file cannot be read at all by an
    // older toolchain, so building this from the wrong config would silently produce an
    // artifact that breaks consumers targeting older Minecraft. Enforce it here rather
    // than relying on remembering.
    doLast {
        val offenders = mutableListOf<String>()
        ZipFile(archiveFile.get().asFile).use { zip ->
            zip.entries().asSequence()
                .filter { it.name.endsWith(".class") }
                .forEach { entry ->
                    zip.getInputStream(entry).use { input ->
                        val header = input.readNBytes(8)
                        if (header.size == 8) {
                            val major = ((header[6].toInt() and 0xFF) shl 8) or (header[7].toInt() and 0xFF)
                            if (major > apiMaxClassFileMajor) {
                                offenders += "${entry.name} (class file major $major)"
                            }
                        }
                    }
                }
        }
        if (offenders.isNotEmpty()) {
            throw GradleException(
                "The API jar contains class files newer than Java 17 (major $apiMaxClassFileMajor), "
                    + "so mods built for older Minecraft versions could not read it. "
                    + "Build it from the oldest configuration that consumers target: "
                    + "-Pminecraft_version_config=$apiSourceVersionConfig\n"
                    + offenders.take(5).joinToString("\n") { "  $it" }
                    + if (offenders.size > 5) "\n  ... and ${offenders.size - 5} more" else ""
            )
        }
    }
}

extensions.configure<PublishingExtension> {
    publications {
        register<MavenPublication>("api") {
            // One artifact for every Minecraft version: the API's public signatures are
            // identical across all of them and reference no Minecraft types, so there is
            // nothing to qualify the coordinate with. The group has to stay lowercase or
            // case-sensitive repository lookups miss it; it is taken from the root project
            // now that that is lowercase there too.
            groupId = rootProject.group.toString()
            artifactId = "superresolution-api"
            version = "${rootProject.property("mod_version")}" + if (srIsDevBuild) "-SNAPSHOT" else ""
            artifact(apiJar) { classifier = null }
            pom {
                name.set("Super Resolution API")
                description.set("Compile-time API for mods extending Super Resolution")
                url.set("https://github.com/187J3X1-114514/superresolution")
                licenses {
                    license {
                        name.set("GNU General Public License v3.0 or later")
                        url.set("https://www.gnu.org/licenses/gpl-3.0.txt")
                    }
                }
            }
        }
    }

    repositories {
        val nexusUser = providers.gradleProperty("shnexusUsername").orNull
        val nexusPassword = providers.gradleProperty("shnexusPassword").orNull
        if (nexusUser != null && nexusPassword != null) {
            maven {
                name = "shnexus"
                // Nexus keeps releases immutable, so development builds have to go to the
                // snapshot repository to stay redeployable.
                url = uri(
                    if (srIsDevBuild) "https://nexus.nyat.icu/repository/maven-snapshots/"
                    else "https://nexus.nyat.icu/repository/maven-releases/"
                )
                credentials {
                    username = nexusUser
                    password = nexusPassword
                }
            }
        }
    }
}
